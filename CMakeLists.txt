# CMakeList.txt: CMake-Projektdatei der obersten Ebene.
#
cmake_minimum_required (VERSION 3.12)
message("Running with CMake Version ${CMAKE_MAJOR_VERSION}.${CMAKE_MINOR_VERSION}.${CMAKE_PATCH_VERSION}")

project ("CARLA OSI client" CXX)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

option(GIT_SUBMODULE "Check submodules during build" ON)
if(GIT_SUBMODULE)
	find_package(Git QUIET)
	if(GIT_FOUND AND EXISTS "${PROJECT_SOURCE_DIR}/.git")
		message(STATUS "Updating submodules")
		execute_process(COMMAND ${GIT_EXECUTABLE} submodule update --init --recursive
						WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
						RESULT_VARIABLE GIT_SUBMODULE_RESULT)
		if(NOT GIT_SUBMODULE_RESULT EQUAL "0")
			message(FATAL_ERROR "git submodule update --init --recursive failed with ${GIT_SUBMODULE_RESULT}")
		endif()
	endif()
endif()

# Download automatically, you can also just copy the conan.cmake file
if(NOT EXISTS "${CMAKE_BINARY_DIR}/conan.cmake")
   message(STATUS "Downloading conan.cmake from https://github.com/conan-io/cmake-conan")
   file(DOWNLOAD "https://github.com/conan-io/cmake-conan/raw/v0.15/conan.cmake"
                 "${CMAKE_BINARY_DIR}/conan.cmake")
endif()

include(${CMAKE_BINARY_DIR}/conan.cmake)

# get dependencies distributed as conan packages
conan_cmake_run(CONANFILE conanfile.txt 
                BASIC_SETUP CMAKE_TARGETS
                BUILD missing)

# include conan packages for protobuf, catch2, fakeit ...
include(${CMAKE_CURRENT_BINARY_DIR}/conanbuildinfo.cmake)

conan_basic_setup()

# Findprotobuf.cmake generated by cmake_find_package conan generator is missing some definitions from the default find script, but catch2 is not found without a defintion for find_package
# => We use our own FindCatch2.cmake module
set(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/CMakeModules ${CMAKE_MODULE_PATH})

# OSI is not distributed as conan package, but we already took care of installig its dependency (protobuf), so we can just use it as submodule
add_subdirectory(lib/open-simulation-interface/ EXCLUDE_FROM_ALL)

# Read CMake_FetchContent_Overrides for changes of FetchContent_declare default of other included scripts (currently applies to FetchLibCarla_client, only)
include(FetchContent)
include(${CMAKE_CURRENT_LIST_DIR}/CMake_FetchContent_Overrides.cmake OPTIONAL)

# use CMake FetchContent Module to fetch LibCarla_client using our custom FetchLibCarla_client.cmake script
include(FetchLibCarla_client)

# use our FetchGRPC.cmake module to add gRPC
include(FetchGRPC)


##main project
# set up variables that are used below
add_subdirectory("src")
add_subdirectory("include")
add_subdirectory("test")


#create library target
add_library(CARLA_OSI_Lib ${CARLA_OSI_LIB_SOURCE} ${CARLA_OSI_LIB_INCLUDES})
# CARLA_OSI_Lib exports no symbols, thereby preventing linking of its dll on windows. Force export of all global symbols
set_property(TARGET CARLA_OSI_Lib PROPERTY WINDOWS_EXPORT_ALL_SYMBOLS ON)
target_include_directories(CARLA_OSI_Lib PUBLIC ${CMAKE_CURRENT_LIST_DIR}/include)

# link to conan managed dependencies
target_link_libraries(CARLA_OSI_Lib PUBLIC CONAN_PKG::protobuf CONAN_PKG::yaml-cpp)

# CARLA is not managed by conan, thus it is added here when enabled
target_link_libraries(CARLA_OSI_Lib PUBLIC LibCarla::LibCarla_client)

# OSI is not managed by conan, thus it is added here
# OSI has differently named targets for static and shared build
if(BUILD_SHARED_LIBS)
	# By default, OSI exports no symbols when building a dll, preventing linking to the shared library on windows. Force export of all global symbols
	set_property(TARGET open_simulation_interface PROPERTY WINDOWS_EXPORT_ALL_SYMBOLS ON)
	target_link_libraries(CARLA_OSI_Lib PUBLIC open_simulation_interface)
	message(STATUS "Will build shared library for OSI")
else()
	target_link_libraries(CARLA_OSI_Lib PUBLIC open_simulation_interface_static)
	message(STATUS "Will build static library for OSI")
endif(BUILD_SHARED_LIBS)

## generate grpc code
# gRPC can handle relative files
set(COSIMA_GRPC_PROTO_FILES
	grpc_proto_files/CoSiMaMessages.proto
	grpc_proto_files/base_interface/BaseInterface.proto
	grpc_proto_files/base_interface/CARLAInterface.proto
)
#string(REPLACE ".proto" ".grpc.pb.cc" COSIMA_GRPC_SRCS ${COSIMA_GRPC_PROTO_FILES})
#string(REPLACE ".proto" ".grpc.pb.h" COSIMA_GRPC_HDRS ${COSIMA_GRPC_PROTO_FILES})
#string(REPLACE ".proto" "_mock.grpc.pb.h" COSIMA_GRPC_MOCK_HDRS ${COSIMA_GRPC_PROTO_FILES})
#string(REPLACE ".proto" ".pb.cc" COSIMA_PROTO_SRCS ${COSIMA_GRPC_PROTO_FILES})
#string(REPLACE ".proto" ".pb.h" COSIMA_PROTO_HDRS ${COSIMA_GRPC_PROTO_FILES})
# using a loop to convert to absolute output file paths
foreach(PROTO_FILE ${COSIMA_GRPC_PROTO_FILES})
	#get_filename_component(PROTO_FILE ${PROTO_FILE} NAME)
	string(REPLACE ".proto" ".grpc.pb.cc" OUT ${PROTO_FILE})
	set(COSIMA_GRPC_SRCS
		${COSIMA_GRPC_SRCS} ${CMAKE_BINARY_DIR}/gens/${OUT})
	string(REPLACE ".proto" ".grpc.pb.h" OUT ${PROTO_FILE})
	set(COSIMA_GRPC_HDRS
		${COSIMA_GRPC_HDRS} ${CMAKE_BINARY_DIR}/gens/${OUT})
	string(REPLACE ".proto" "_mock.grpc.pb.h" OUT ${PROTO_FILE})
	set(COSIMA_GRPC_MOCK_HDRS
		${COSIMA_GRPC_MOCK_HDRS} ${CMAKE_BINARY_DIR}/gens/${OUT})
	string(REPLACE ".proto" ".pb.cc" OUT ${PROTO_FILE})
	set(COSIMA_PROTO_SRCS
		${COSIMA_PROTO_SRCS} ${CMAKE_BINARY_DIR}/gens/${OUT})
	string(REPLACE ".proto" ".pb.h" OUT ${PROTO_FILE})
	set(COSIMA_PROTO_HDRS
		${COSIMA_PROTO_HDRS} ${CMAKE_BINARY_DIR}/gens/${OUT})
endforeach()

# add grpc dependency
set(_gRPC_PROTO_GENS_DIR ${CMAKE_BINARY_DIR}/gens)
protobuf_generate_grpc_cpp(${COSIMA_GRPC_PROTO_FILES})
add_library(CARLA_OSI_gRPC_Lib ${COSIMA_PROTO_SRCS} ${COSIMA_GRPC_SRCS})
target_link_libraries(CARLA_OSI_gRPC_Lib PUBLIC grpc++)
target_include_directories(CARLA_OSI_gRPC_Lib BEFORE PUBLIC ${CMAKE_CURRENT_LIST_DIR}/include ${CMAKE_BINARY_DIR}/gens)

# gRPC service logic library, separated from CARLA_OSI_Client to be testalble with catch2
add_library(CARLA_OSI_gRPC_ServiceLib "${CMAKE_CURRENT_LIST_DIR}/src/CARLA_OSI_gRPC.cpp" "${CMAKE_CURRENT_LIST_DIR}/include/CARLA_OSI_gRPC.h")
target_link_libraries(CARLA_OSI_gRPC_ServiceLib PUBLIC CARLA_OSI_gRPC_Lib CARLA_OSI_Lib)
target_include_directories(CARLA_OSI_gRPC_Lib BEFORE PUBLIC ${CMAKE_CURRENT_LIST_DIR}/include ${CMAKE_BINARY_DIR}/gens)

find_package(Catch2 REQUIRED)
add_executable(Test_CARLA_OSI ${TESTFILES})
target_include_directories(Test_CARLA_OSI PRIVATE ${CMAKE_CURRENT_LIST_DIR}/include)
target_link_libraries(Test_CARLA_OSI Catch2::Catch2 CARLA_OSI_Lib CARLA_OSI_gRPC_ServiceLib)

include(CTest)
include(Catch)
catch_discover_tests(Test_CARLA_OSI)

add_executable (CARLA_OSI_Client "${CMAKE_CURRENT_LIST_DIR}/src/CARLA_OSI_Client.cpp")
target_link_libraries(CARLA_OSI_Client PUBLIC CARLA_OSI_Lib)
target_include_directories(CARLA_OSI_Client PRIVATE ${CMAKE_CURRENT_BINARY_DIR}/include/)
target_link_libraries(CARLA_OSI_Client PUBLIC CARLA_OSI_gRPC_ServiceLib)
# public include of target CARLA_OSI_gRPC_Lib is supposed to propagate, but is missing...
target_include_directories(CARLA_OSI_Client BEFORE PUBLIC ${CMAKE_BINARY_DIR}/gens)

# The import section in conanfile.txt is supposed to do the same, but somehow it stopped working.
# so far, only boost is needed. Additional globbing expressions can be added if needed
file(GLOB CONAN_BOOST_LIBS ${CONAN_BIN_DIRS_BOOST}/*.dll )
file(COPY ${CONAN_BOOST_LIBS} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/bin)