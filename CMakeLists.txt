# CMakeList.txt: CMake-Projektdatei der obersten Ebene.
#
cmake_minimum_required (VERSION 3.12)
message("Running with CMake Version ${CMAKE_MAJOR_VERSION}.${CMAKE_MINOR_VERSION}.${CMAKE_PATCH_VERSION}")

project ("CoSimulationManager" CXX)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

option(GIT_SUBMODULE "Check submodules during build" ON)
if(GIT_SUBMODULE)
	find_package(Git QUIET)
	if(GIT_FOUND AND EXISTS "${PROJECT_SOURCE_DIR}/.git")
		message(STATUS "Updating submodules")
		execute_process(COMMAND ${GIT_EXECUTABLE} submodule update --init --recursive
						WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
						RESULT_VARIABLE GIT_SUBMODULE_RESULT)
		if(NOT GIT_SUBMODULE_RESULT EQUAL "0")
			message(FATAL_ERROR "git submodule update --init --recursive failed with ${GIT_SUBMODULE_RESULT}")
		endif()
	endif()
endif()

# Download automatically, you can also just copy the conan.cmake file
if(NOT EXISTS "${CMAKE_BINARY_DIR}/conan.cmake")
   message(STATUS "Downloading conan.cmake from https://github.com/conan-io/cmake-conan")
   file(DOWNLOAD "https://github.com/conan-io/cmake-conan/raw/v0.15/conan.cmake"
                 "${CMAKE_BINARY_DIR}/conan.cmake")
endif()

include(${CMAKE_BINARY_DIR}/conan.cmake)

# get dependencies distributed as conan packages
conan_cmake_run(CONANFILE conanfile.txt 
                BASIC_SETUP CMAKE_TARGETS
                BUILD missing)

# Using the conanfile.txt provided by FMI4cpp prevents overriding requirements, but Carla needs a newer boost library
	# FMI4cpp is not distributed as conan package, but still can use conan for dependency management.
	#conan_cmake_run(CONANFILE lib/FMI4cpp/conanfile.txt
	#                INSTALL_FOLDER "${CMAKE_BINARY_DIR}/lib/FMI4cpp/"
	#                BASIC_SETUP
	#                BUILD missing)
	# FMI4CPP_USING_CONAN is faulty because it doesn't use the correct binary dir
	#set(FMI4CPP_USING_CONAN ON CACHE BOOL "Build FMI4cpp using conan" FORCE)
	#include(${CMAKE_CURRENT_BINARY_DIR}/lib/FMI4cpp/conanbuildinfo.cmake)
conan_basic_setup()
add_subdirectory(lib/FMI4cpp/ EXCLUDE_FROM_ALL)

# include protobuf, yaml_cpp, catch2, fakeit
include(${CMAKE_CURRENT_BINARY_DIR}/conanbuildinfo.cmake)
# needed by cmake_find_package conan generator, but breaks protobuf
#include(${CMAKE_CURRENT_BINARY_DIR}/conan_paths.cmake)
conan_basic_setup()
# Findprotobuf.cmake generated by cmake_find_package conan generator is missing some definitons from the default find script, but catch2 is not found without a defintion for find_package
# => We use our own FindCatch2.cmake module
set(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/CMakeModules ${CMAKE_MODULE_PATH})

# OSI is not distributed as conan package, but we already took care of installig its dependency (protobuf), so we can just use it as submodule
add_subdirectory(lib/open-simulation-interface/ EXCLUDE_FROM_ALL)

option(WITH_CARLA "Fetch Carla and non-conan dependencies and build base simulator interface based on LibCarla_client" ON)
if(WITH_CARLA)
	message(VERBOSE "Will add Carla base interface to build")
	message("CMAKE_CXX_FLAGS_RELEASE ${CMAKE_CXX_FLAGS_RELEASE}")
	message("CMAKE_CXX_FLAGS_DEBUG ${CMAKE_CXX_FLAGS_DEBUG}")
	include(FetchLibCarla_client)
	
	#fetch_carla_and_non_conan_dependencies($<CONFIG:Debug>)
	fetch_carla_and_non_conan_dependencies(FALSE)# always use release build of carla client
endif()


##main project
# set up variables that are used below
add_subdirectory("src")
add_subdirectory("include")
add_subdirectory("test")


#create library target
add_library(CoSimulationManagerLib ${COSIMA_SOURCE} ${COSIMA_INCLUDES})
# CoSimulationManagerLib exports no symbols, thereby preventing linking of its dll on windows. Force export of all global symbols
set_property(TARGET CoSimulationManagerLib PROPERTY WINDOWS_EXPORT_ALL_SYMBOLS ON)
target_include_directories(CoSimulationManagerLib PUBLIC ${CMAKE_CURRENT_LIST_DIR}/include)

# link to conan managed dependencies
target_link_libraries(CoSimulationManagerLib PUBLIC ${CONAN_LIBS})

# FMI4cpp is not managed by conan, thus it is added here
target_link_libraries(CoSimulationManagerLib PUBLIC fmi4cpp)

if(WITH_CARLA)
	# CARLA is not managed by conan, thus it is added here when enabled
	target_link_libraries(CoSimulationManagerLib PUBLIC LibCarla::LibCarla_client)

	get_target_property(_CARLA_INCLUDES LibCarla::LibCarla_client INTERFACE_INCLUDE_DIRECTORIES)
	message("LibCarla INTERFACE_INCLUDE_DIRECTORIES: ${_CARLA_INCLUDES}")
endif(WITH_CARLA)

# OSI is not managed by conan, thus it is added here
# OSI has differently named targets for static and shared build
if(BUILD_SHARED_LIBS)
	# By default, OSI exports no symbols when building a dll, preventing linking to the shared library on windows. Force export of all global symbols
	set_property(TARGET open_simulation_interface PROPERTY WINDOWS_EXPORT_ALL_SYMBOLS ON)
	target_link_libraries(CoSimulationManagerLib PUBLIC open_simulation_interface)
else()
	target_link_libraries(CoSimulationManagerLib PUBLIC open_simulation_interface_static)
endif(BUILD_SHARED_LIBS)

get_target_property(OUT CoSimulationManagerLib INCLUDE_DIRECTORIES)
message("INCLUDE_DIRECTORIES: ${OUT}")
get_target_property(OUT CoSimulationManagerLib INTERFACE_INCLUDE_DIRECTORIES)
message("INTERFACE_INCLUDE_DIRECTORIES: ${OUT}")

find_package(Catch2 REQUIRED)
add_executable(test_CoSiMa ${TESTFILES})
target_include_directories(test_CoSiMa PRIVATE ${CMAKE_CURRENT_LIST_DIR}/include)
target_link_libraries(test_CoSiMa CoSimulationManagerLib ${CONAN_LIBS} Catch2::Catch2)

#copy test resources to build folder
add_custom_command(TARGET test_CoSiMa PRE_BUILD
                     COMMAND ${CMAKE_COMMAND} -E
                         make_directory $<TARGET_FILE_DIR:test_CoSiMa>/../test/resources/)
#Feedthrough_cs_me.fmu contains definitions for both me and cs => created copies with edited modelDescription.xml
add_custom_command(TARGET test_CoSiMa PRE_BUILD
                     COMMAND ${CMAKE_COMMAND} -E
                         copy_if_different
						 ${CMAKE_CURRENT_LIST_DIR}/test/resources/testconfig1.yaml
						 ${CMAKE_CURRENT_LIST_DIR}/test/resources/Feedthrough_cs.fmu
						 ${CMAKE_CURRENT_LIST_DIR}/test/resources/Feedthrough_me.fmu
						 ${CMAKE_CURRENT_LIST_DIR}/test/resources/Stair.fmu
						 $<TARGET_FILE_DIR:test_CoSiMa>/../test/resources/)

include(CTest)
include(Catch)
catch_discover_tests(test_CoSiMa)


add_executable (CoSimulationManager "${CMAKE_CURRENT_LIST_DIR}/src/CoSiMa.cpp" "${CMAKE_CURRENT_LIST_DIR}/include/CoSiMa.h")
target_link_libraries(CoSimulationManager CoSimulationManagerLib)